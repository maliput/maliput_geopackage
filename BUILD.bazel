###############################################################################
# Loaders
###############################################################################

load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library")
load("//:bazel/variables.bzl", "COPTS")

###############################################################################
# Libraries
###############################################################################

# Aggregate library for maliput_geopackage.
cc_library(
    name = "maliput_geopackage",
    visibility = ["//visibility:public"],
    deps = [
        ":builder",
        ":geopackage",
    ],
)

# Private geopackage parser headers (internal implementation)
cc_library(
    name = "private_geopackage_headers",
    hdrs = glob(["src/maliput_geopackage/geopackage/*.h"]),
    copts = COPTS,
    strip_include_prefix = "src",
    visibility = ["//:__pkg__"],
    deps = [
        "@maliput//:common",
        "@maliput_sparse//:parser",
        "@sqlite3",
    ],
)

# GeoPackage parser library
cc_library(
    name = "geopackage",
    srcs = glob(["src/maliput_geopackage/geopackage/*.cc"]),
    copts = COPTS,
    visibility = ["//:__pkg__"],
    deps = [
        ":private_geopackage_headers",
        "@maliput//:common",
        "@maliput//:math",
        "@maliput_sparse//:geometry",
        "@maliput_sparse//:parser",
        "@sqlite3",
    ],
)

# Private builder headers (internal implementation)
cc_library(
    name = "private_builder_headers",
    hdrs = glob(["src/maliput_geopackage/builder/*.h"]),
    copts = COPTS,
    strip_include_prefix = "src",
    visibility = [
        "//:__pkg__",
        "//test:__subpackages__",
    ],
    deps = [
        "@maliput//:api",
        "@maliput//:math",
        "@maliput_sparse//:loader",
    ],
)

# Builder library
cc_library(
    name = "builder",
    srcs = glob(["src/maliput_geopackage/builder/*.cc"]),
    hdrs = glob(["include/maliput_geopackage/builder/*.h"]),
    copts = COPTS,
    strip_include_prefix = "include",
    visibility = ["//visibility:public"],
    deps = [
        ":geopackage",
        ":private_builder_headers",
        "@maliput//:api",
        "@maliput//:common",
        "@maliput_sparse//:loader",
    ],
)

cc_binary(
    # Road network plugin for the maliput_geopackage implementation.
    # - linkstatic is set to True to link all the dependencies statically.
    # This is convenient for users of the plugin, as they won't have to
    # worry about providing the dependencies at runtime.
    # - linkshared is set to True to build a shared library.
    name = "maliput_plugins/libmaliput_geopackage_road_network.so",
    srcs = glob(["src/plugin/**/*.cc"]),
    copts = COPTS,
    visibility = ["//visibility:public"],
    deps = [
        ":builder",
        "@maliput//:plugin",
    ],
    linkstatic = True,
    linkshared = True,
)
